buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
    id 'org.ajoberstar.grgit' version '2.1.1'
}

group = "com.blackfez"
version = "0.1"

def currentBranch = '' 
def sbout = new StringBuilder()
def sberr = new StringBuilder()
def process = "git branch".execute()
process.consumeProcessOutput( sbout, sberr )
process.waitForOrKill( 1000 )
sbout.eachLine {
    if( it.trim().startsWith( '*' ) )
        currentBranch = it.trim().split( ' ' )[ 1 ]
}

def buildnumber = "LOCAL"
def wart = ""

if( System.env.BRANCH_NAME ) {
    currentBranch = System.env.BRANCH_NAME
    buildnumber = System.env.BUILD_NUMBER
}

if( currentBranch != 'master' ) {
    wart = currentBranch + "."
}

version = version + "." + wart + buildnumber

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'
}

bintray {
    user = System.getenv( 'BINTRAY_USER' )
    key = System.getenv( 'BINTRAY_KEY' )
    configurations = ['archives']
    pkg {
        repo = 'blackfezjava'
        name = 'fezcore'
        userOrg = user
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/n1ckhansen/FezCore.git'
        version {
            name = project.property( 'version' ) 
            released = new Date()
        }
    }
}


dependencies {
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

test {
    systemProperties 'property': 'value'
}

task sourcesJar( type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar( type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The GNU Public License, Version 3.0"
            url "http://www.gnu.org/licenses/gpl-3.0.en.txt"
            distribution "blackfezjava"
        }
    }
    developers {
        developer {
            id "n1ckhansen"
            name "Nick Hansen"
            email "nhansen@blackfez.com"
        }
    }
    scm {
        url "https://github.com/n1ckhansen/FezCore"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.blackfez'
            artifactId 'fezcore'
            version project.property( 'version' )
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Core reusable classes in the BFP mold')
                root.appendNode('name', 'FezCore')
                root.appendNode('url', 'https://github.com/n1ckhansen/fezcore')
                root.children().last() + pomConfig
            }
        }
    }
}
